buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    //dependencies {
    //  classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:1.0.1'
    //}
}

plugins {
    id 'com.commercehub.gradle.plugin.avro' version '0.8.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'com.github.johnrengelman.shadow'
//apply plugin: 'scalaStyle'
apply plugin: 'com.commercehub.gradle.plugin.avro'
apply from: 'spark.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// The main class for the application plugin and is currently unused. Setting a dummy one.
mainClassName = 'com.salesforce.app.SummaryModel'

ext {
    scalaVersion = '2.11'
    scalaVersionRevision = '12'
    junitVersion = '4.11'
    sparkVersion = '2.3.2'
    scalaTestVersion = '3.0.5'
    transmogrifaiVersion = '0.5.2'
    collectionsVersion = '3.2.2'

    mainClassName = 'com.salesforce.app.SummaryModel'
}

configurations {
    scalaLibrary
    scalaCompiler
}

dependencies {
    // Scala
    zinc 'com.typesafe.zinc:zinc:0.3.15'
    compile "org.scala-lang:scala-library:2.11.12"
    compile "org.scala-lang:scala-compiler:2.11.12"
    compile "org.scala-lang:scala-library:2.11.12"

    // Spark
    compile "org.apache.spark:spark-core_2.11:2.3.2"
    compile "org.apache.spark:spark-mllib_2.11:2.3.2"
    compile "org.apache.spark:spark-sql_2.11:2.3.2"

    // TransmogrifAI
    compile "com.salesforce.transmogrifai:transmogrifai-core_2.11:0.5.2"

    // Pretrained models used in TransmogrifAI, e.g. OpenNLP POS/NER models etc. (optional)
    // compile "com.salesforce.transmogrifai:transmogrifai-models_2.11:0.5.2"

    // Test
    testCompile "org.scalatest:scalatest_2.11:3.0.5"
    testCompile "junit:junit:4.11"

    // Avro
    compileOnly("org.apache.avro:avro:1.7.7") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
    testCompile("org.apache.avro:avro:1.7.7") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
    compileOnly("org.apache.avro:avro-mapred:1.7.7:hadoop2") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }
    testCompile("org.apache.avro:avro-mapred:1.7.7:hadoop2") { exclude group: 'org.mortbay.jetty', module: 'servlet-api' }

    // Spark Avro
    compile ("com.databricks:spark-avro_2.11:4.0.0") { exclude group: "org.apache.avro", module: "avro" }

}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
        force "commons-collections:commons-collections:3.2.2",
                "org.scala-lang:scala-library:2.11.12",
                "org.scala-lang:scala-reflect:2.11.12"
    }
}
configurations.zinc {
    resolutionStrategy {
        force 'org.scala-lang:scala-library:2.10.6', 'org.scala-lang:scala-reflect:2.10.6'
    }
}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxMetaspaceSize=256m']
    }
}
compileScala { scalaCompileOptions.additionalParameters = ["-optimize"] }
compileTestScala { scalaCompileOptions.additionalParameters = ["-Yrangepos"] }
[compileJava, compileTestJava]*.options.collect { options -> options.encoding = 'UTF-8' }

jar {
    manifest.attributes "Main-Class": "$mainClassName"
    baseName="${rootProject.name}"
}

shadowJar {
    zip64 = true
    exclude 'META-INF/**'
    manifest.attributes "Main-Class": "$mainClassName"
    baseName = project.name + '-with-dependencies'
}

avro {
    createSetters = true
    fieldVisibility = "PUBLIC_DEPRECATED"
    outputCharacterEncoding = "UTF-8"
    stringType = "String"
}

sourceSets {
    main {
        java {
            srcDir 'build/generated-main-avro-java'
        }
    }
}

task repl(type: JavaExec) {
    description 'Start Scala repl.'
    main = "scala.tools.nsc.MainGenericRunner"
    classpath = sourceSets.main.runtimeClasspath
    standardInput System.in
    args '-usejavacp'
}

wrapper {
    gradleVersion = '5.2'
    distributionType = Wrapper.DistributionType.BIN
    distributionSha256Sum = 'ff322863250159595e93b5a4d17a6f0d21c59a1a0497c1e1cf1d53826485503f'
}
